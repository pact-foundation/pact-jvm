plugins {
  id 'au.com.dius.pact.kotlin-application-conventions'
  id 'scala'
  id 'maven-publish'
  id 'signing'
}

group = 'au.com.dius.pact'
mainClassName = 'au.com.dius.pact.server.Server'

dependencies {
  implementation project(':consumer')
  implementation project(':core:pactbroker')
  implementation 'ch.qos.logback:logback-core:1.4.4'
  implementation 'ch.qos.logback:logback-classic:1.4.4'
  implementation 'com.github.scopt:scopt_2.12:3.5.0'
  implementation('com.typesafe.scala-logging:scala-logging_2.12:3.7.2') {
      exclude group: 'org.scala-lang'
  }
  implementation( 'ws.unfiltered:unfiltered-netty-server_2.12:0.10.4') {
    exclude module: 'netty-transport-native-kqueue'
    exclude module: 'netty-transport-native-epoll'
  }
  implementation 'org.apache.commons:commons-io:1.3.2'
  implementation 'org.apache.tika:tika-core'
  implementation 'org.apache.commons:commons-lang3'

  testImplementation 'org.apache.groovy:groovy'
  testImplementation 'org.apache.groovy:groovy-json'
  testImplementation platform("org.spockframework:spock-bom:2.3-groovy-4.0")
  testImplementation 'org.spockframework:spock-core'
  testRuntimeOnly 'net.bytebuddy:byte-buddy'
}

jar {
    manifest.attributes 'Main-Class': mainClassName,
            'Class-Path': configurations.compileClasspath.collect { it.getName() }.join(' ')
}

java {
  withJavadocJar()
  withSourcesJar()
}

test {
  dependsOn(':pact-jvm-server:assembleDist')
  systemProperty('appExecutable', (new File(buildDir, 'install/pact-jvm-server/bin/pact-jvm-server')).path)
}

publishing {
  publications {
    serverDistribution(MavenPublication) {
      from components.java
      artifact distZip
      artifact distTar
      pom {
        name = project.name
        packaging = 'jar'
        description = 'Stand-alone Pact server'
        url = 'https://github.com/pact-foundation/pact-jvm'
        licenses {
          license {
            name = 'Apache 2'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }
        scm {
          url = 'https://github.com/pact-foundation/pact-jvm'
          connection = 'https://github.com/pact-foundation/pact-jvm.git'
        }

        developers {
          developer {
            id = 'thetrav'
            name = 'Travis Dixon'
            email = 'the.trav@gmail.com'
          }
          developer {
            id = 'rholshausen'
            name = 'Ronald Holshausen'
            email = 'ronald.holshausen@gmail.com'
          }
        }
      }
    }
  }
  repositories {
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      if (project.hasProperty('sonatypeUsername')) {
        credentials {
          username sonatypeUsername
          password sonatypePassword
        }
      }
    }
  }
}

signing {
  required { project.hasProperty('isRelease') }
  sign publishing.publications.serverDistribution
}
