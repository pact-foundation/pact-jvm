plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm'

    id 'groovy'
    id "io.gitlab.arturbosch.detekt"
    id 'codenarc'
}

repositories {
    mavenLocal()
    mavenCentral()
}

version = '4.6.18'

java {
    targetCompatibility = '17'
    sourceCompatibility = '17'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "17"
    }
}

dependencies {
    constraints {
        // Define dependency versions as constraints
        api 'org.apache.httpcomponents.client5:httpclient5:5.4.4'
        api 'org.apache.httpcomponents.client5:httpclient5-fluent:5.4.4'
        api 'org.json:json:20250517'
        api 'io.pact.plugin.driver:core:0.5.2' // remember also to update implementation
        api 'org.apache.tika:tika-core:2.9.4'
        api 'io.github.oshai:kotlin-logging-jvm:6.0.9'

        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
        implementation 'org.apache.commons:commons-lang3:3.19.0'
        implementation 'org.apache.commons:commons-text:1.14.0'
        implementation 'org.apache.commons:commons-collections4:4.5.0'
        implementation 'org.apache.tika:tika-core:2.9.4'
        implementation 'com.google.guava:guava:33.5.0-jre'
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'io.ktor:ktor-http-jvm:2.3.13'
        implementation 'io.ktor:ktor-server-netty:2.3.13'
        implementation 'io.ktor:ktor-network-tls-certificates:2.3.13'
        implementation 'io.ktor:ktor-server-call-logging:2.3.13'
        implementation 'io.netty:netty-handler:4.1.128.Final'
        implementation 'org.apache.groovy:groovy:4.0.29'
        implementation 'org.apache.groovy:groovy-json:4.0.29'
        implementation 'org.apache.groovy:groovy-xml:4.0.29'
        implementation 'io.pact.plugin.driver:core:0.5.2'  //remember to also update under api
        implementation 'commons-codec:commons-codec:1.19.0'
        implementation 'io.github.oshai:kotlin-logging-jvm:6.0.9'

        testImplementation 'org.apache.groovy:groovy:4.0.29'
        testImplementation 'org.apache.groovy:groovy-json:4.0.29'
        testImplementation 'org.apache.groovy:groovy-datetime:4.0.29'
        testImplementation 'org.apache.groovy:groovy-xml:4.0.29'
        testImplementation 'org.apache.groovy:groovy-nio:4.0.29'
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'ch.qos.logback:logback-classic:1.4.14'
        testImplementation 'ch.qos.logback:logback-core:1.4.14'
        testImplementation 'net.bytebuddy:byte-buddy:1.17.8'
        testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.2'
        testImplementation 'org.junit.vintage:junit-vintage-engine:5.11.4'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    }

    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    codenarc('org.codenarc:CodeNarc:3.1.0-groovy-4.0')
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.11.4')
        }
    }
}

test {
    // Mark sure Pact-JVM builds don't skew the metrics
    systemProperty 'pact_do_not_track', 'true'
}

tasks.withType(CodeNarc).configureEach { codeNarcTask ->
    reports {
        console {
            required = true
        }
    }
}

codenarcMain {
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
}

codenarcTest {
    configFile = rootProject.file('config/codenarc/rulesetTest.groovy')
}

detekt {
    failFast = false
    config = files(rootProject.file("config/detekt-config.yml"))
}
