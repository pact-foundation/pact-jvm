plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm'

    id 'groovy'
    // id "io.gitlab.arturbosch.detekt" TODO: Detekt does not support JDK 23 yet
    id 'codenarc'
}

repositories {
    mavenLocal()
    mavenCentral()
}

version = '4.7.0-beta.0'

java {
    targetCompatibility = '17'
    sourceCompatibility = '17'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "17"
    }
}

dependencies {
    constraints {
        // Define dependency versions as constraints
        api 'org.apache.httpcomponents.client5:httpclient5:5.3.1'
        api 'org.apache.httpcomponents.client5:httpclient5-fluent:5.3.1'
        api 'org.json:json:20240205'
        api 'io.pact.plugin.driver:core:0.5.1' // remember also to update implementation
        api 'org.apache.tika:tika-core:2.9.1'
        api 'io.github.oshai:kotlin-logging-jvm:5.1.4'

        implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.0.21'
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'org.apache.commons:commons-text:1.10.0'
        implementation 'org.apache.commons:commons-collections4:4.4'
        implementation 'org.apache.tika:tika-core:2.9.1'
        implementation 'com.google.guava:guava:31.1-jre'
        implementation 'org.slf4j:slf4j-api:1.7.36'
        implementation 'io.ktor:ktor-http-jvm:2.3.8'
        implementation 'io.ktor:ktor-server-netty:2.3.8'
        implementation 'io.ktor:ktor-network-tls-certificates:2.3.8'
        implementation 'io.ktor:ktor-server-call-logging:2.3.8'
        implementation 'io.netty:netty-handler:4.1.108.Final'
        implementation 'org.apache.groovy:groovy:4.0.23'
        implementation 'org.apache.groovy:groovy-json:4.0.23'
        implementation 'org.apache.groovy:groovy-xml:4.0.23'
        implementation 'io.pact.plugin.driver:core:0.5.1'  //remember to also update under api
        implementation 'commons-codec:commons-codec:1.15'
        implementation 'io.github.oshai:kotlin-logging-jvm:5.1.4'

        testImplementation 'org.apache.groovy:groovy:4.0.23'
        testImplementation 'org.apache.groovy:groovy-json:4.0.23'
        testImplementation 'org.apache.groovy:groovy-datetime:4.0.23'
        testImplementation 'org.apache.groovy:groovy-xml:4.0.23'
        testImplementation 'org.apache.groovy:groovy-nio:4.0.23'
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'ch.qos.logback:logback-classic:1.4.5'
        testImplementation 'ch.qos.logback:logback-core:1.4.5'
        testImplementation 'net.bytebuddy:byte-buddy:1.15.7'
        testImplementation 'com.github.tomakehurst:wiremock-jre8:2.34.0'
        testImplementation 'org.junit.vintage:junit-vintage-engine:5.11.3'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    }

    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom:2.0.21')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.0.21'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:2.0.21'

    codenarc('org.codenarc:CodeNarc:3.5.0-groovy-4.0')
    codenarc('org.apache.groovy:groovy:4.0.23')
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.11.3')
        }
    }
}

test {
    // Mark sure Pact-JVM builds don't skew the metrics
    systemProperty 'pact_do_not_track', 'true'
}

tasks.withType(CodeNarc).configureEach { codeNarcTask ->
    reports {
        console {
            required = true
        }
    }
}

codenarcMain {
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
}

codenarcTest {
    configFile = rootProject.file('config/codenarc/rulesetTest.groovy')
}

// TODO: Detekt does not support JDK 23 yet
//detekt {
//    config = files(rootProject.file("config/detekt-config.yml"))
//}
