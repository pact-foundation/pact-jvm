plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm'

    id 'groovy'
    id "io.gitlab.arturbosch.detekt"
    id 'codenarc'
}

repositories {
    mavenLocal()
    mavenCentral()
}

version = '4.5.10'

java {
    targetCompatibility = '11'
    sourceCompatibility = '11'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

dependencies {
    constraints {
        // Define dependency versions as constraints
        api 'org.apache.httpcomponents.client5:httpclient5:5.1.3'
        api 'org.apache.httpcomponents.client5:httpclient5-fluent:5.1.3'
        api 'org.json:json:20230618'
        api 'io.pact.plugin.driver:core:0.3.2'
        api 'org.apache.tika:tika-core:1.28.5'
        
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'org.apache.commons:commons-text:1.10.0'
        implementation 'org.apache.commons:commons-collections4:4.4'
        implementation 'org.apache.tika:tika-core:1.28.5'
        implementation 'com.google.guava:guava:30.1.1-jre'
        implementation 'org.slf4j:slf4j-api:1.7.32'
        implementation 'io.ktor:ktor-http-jvm:1.6.8'
        implementation 'io.ktor:ktor-server-netty:1.6.8'
        implementation 'io.ktor:ktor-network-tls-certificates:1.6.8'
        implementation 'io.netty:netty-handler:4.1.84.Final'
        implementation 'org.apache.groovy:groovy:4.0.11'
        implementation 'org.apache.groovy:groovy-json:4.0.11'
        implementation 'org.apache.groovy:groovy-xml:4.0.11'
        implementation 'io.pact.plugin.driver:core:0.3.2'

        testImplementation 'org.apache.groovy:groovy:4.0.11'
        testImplementation 'org.apache.groovy:groovy-json:4.0.11'
        testImplementation 'org.apache.groovy:groovy-datetime:4.0.11'
        testImplementation 'org.apache.groovy:groovy-xml:4.0.11'
        testImplementation 'org.apache.groovy:groovy-nio:4.0.11'
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'ch.qos.logback:logback-classic:1.4.4'
        testImplementation 'ch.qos.logback:logback-core:1.4.4'
        testImplementation 'net.bytebuddy:byte-buddy:1.12.7'
        testImplementation 'com.github.tomakehurst:wiremock-jre8:2.34.0'
        testImplementation 'org.junit.vintage:junit-vintage-engine:5.9.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    }

    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    codenarc('org.codenarc:CodeNarc:3.1.0-groovy-4.0')
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.9.2')
        }
    }
}

test {
    // Mark sure Pact-JVM builds don't skew the metrics
    systemProperty 'pact_do_not_track', 'true'
}

tasks.withType(CodeNarc).configureEach { codeNarcTask ->
    reports {
        console {
            required = true
        }
    }
}

codenarcMain {
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
}

codenarcTest {
    configFile = rootProject.file('config/codenarc/rulesetTest.groovy')
}

detekt {
    failFast = false
    config = files(rootProject.file("config/detekt-config.yml"))
}
