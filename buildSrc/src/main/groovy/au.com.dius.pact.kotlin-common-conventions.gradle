plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm'

    id 'groovy'
    id 'codenarc'
}

// Detekt blows up on JDK 23
if (JavaVersion.current() < JavaVersion.VERSION_23) {
    project.plugins.apply('io.gitlab.arturbosch.detekt')
}

repositories {
    mavenLocal()
    mavenCentral()
}

version = '4.7.0-beta.2'

java {
    targetCompatibility = '17'
    sourceCompatibility = '17'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "17"
    }
}

dependencies {
    constraints {
        // Define dependency versions as constraints
        api 'org.apache.httpcomponents.client5:httpclient5:5.4.4'
        api 'org.apache.httpcomponents.client5:httpclient5-fluent:5.4.4'
        api 'org.json:json:20250517'
        api 'io.pact.plugin.driver:core:0.5.2' // remember also to update implementation
        api 'org.apache.tika:tika-core:2.9.4'
        api 'io.github.oshai:kotlin-logging-jvm:7.0.7'

        implementation 'commons-codec:commons-codec:1.19.0'
        implementation 'commons-io:commons-io:2.19.0'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.2.0'
        implementation 'org.apache.commons:commons-lang3:3.19.0'
        implementation 'org.apache.commons:commons-text:1.14.0'
        implementation 'org.apache.commons:commons-collections4:4.5.0'
        implementation 'org.apache.tika:tika-core:2.9.4'
        implementation 'com.google.guava:guava:33.5.0-jre'
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'io.ktor:ktor-http-jvm:3.1.3'
        implementation 'io.ktor:ktor-server-netty:3.1.3'
        implementation 'io.ktor:ktor-network-tls-certificates:3.1.3'
        implementation 'io.ktor:ktor-server-call-logging:3.1.3'
        implementation 'io.netty:netty-handler:4.2.1.Final'
        implementation 'org.apache.groovy:groovy:4.0.29'
        implementation 'org.apache.groovy:groovy-json:4.0.29'
        implementation 'org.apache.groovy:groovy-xml:4.0.29'
        implementation 'io.pact.plugin.driver:core:0.5.2'  //remember to also update under api
        implementation 'io.github.oshai:kotlin-logging-jvm:7.0.7'

        testImplementation 'org.apache.groovy:groovy:4.0.29'
        testImplementation 'org.apache.groovy:groovy-json:4.0.29'
        testImplementation 'org.apache.groovy:groovy-datetime:4.0.29'
        testImplementation 'org.apache.groovy:groovy-xml:4.0.29'
        testImplementation 'org.apache.groovy:groovy-nio:4.0.29'
        testImplementation 'org.hamcrest:hamcrest:3.0'
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'ch.qos.logback:logback-classic:1.4.14'
        testImplementation 'ch.qos.logback:logback-core:1.4.14'
        testImplementation 'net.bytebuddy:byte-buddy:1.17.8'
        testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.2'
        testImplementation 'org.junit.vintage:junit-vintage-engine:5.12.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.2'
    }

    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom:2.2.0')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.2.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:2.2.0'

    codenarc('org.codenarc:CodeNarc:3.6.0-groovy-4.0')
    codenarc('org.apache.groovy:groovy:4.0.29')
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.12.2')
        }
    }
}

test {
    // Mark sure Pact-JVM builds don't skew the metrics
    systemProperty 'pact_do_not_track', 'true'
}

tasks.withType(CodeNarc).configureEach { codeNarcTask ->
    reports {
        console {
            required = true
        }
    }
}

codenarcMain {
    configFile = rootProject.file('config/codenarc/ruleset.groovy')
}

codenarcTest {
    configFile = rootProject.file('config/codenarc/rulesetTest.groovy')
}

if (JavaVersion.current() < JavaVersion.VERSION_23) {
    detekt {
        config = files(rootProject.file("config/detekt-config.yml"))
    }
}
