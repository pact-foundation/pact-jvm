buildscript {
  repositories {
    maven { url 'https://clojars.org/repo' }
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath 'org.apache.commons:commons-lang3:3.10'
    classpath 'org.clojure:clojure:1.10.1'
  }
}

plugins {
  id 'au.com.dius.pact.kotlin-library-conventions'
  id "com.netflix.nebula.clojure" version "14.2.1"
}

description = 'Pact-JVM - Leiningen Provider test support library'
group = 'au.com.dius.pact.provider'

import org.apache.commons.lang3.RandomStringUtils

dependencies {
  api project(":provider")
  implementation 'org.clojure:clojurescript:1.11.132'
  implementation 'org.clojure:core.match:1.1.0'
  implementation 'org.clojure:core.rrb-vector:0.2.0'
  implementation 'leiningen-core:leiningen-core:2.9.10'
  implementation 'org.apache.maven:maven-aether-provider:3.0.5'
  implementation 'org.sonatype.aether:aether-connector-file:1.13.1'
  implementation 'org.sonatype.aether:aether-connector-wagon:1.13.1'
  implementation 'org.apache.httpcomponents.client5:httpclient5'
  implementation 'org.apache.groovy:groovy'

  testImplementation 'org.clojure:tools.nrepl:0.2.13'

  groovyDoc 'org.apache.groovy:groovy-all:4.0.11'
}

clojure.aotCompile = true

compileClojure {
  dependsOn compileGroovy
  classpath.from(files(compileGroovy.destinationDirectory))
  destinationDir = file("${project.buildDir}/classes/java/main")
}

clojureTest {
  junit = true
  classpath.from(files(compileGroovy.destinationDirectory))
  junitOutputDir = file("$buildDir/test-results/clojure/" + RandomStringUtils.randomAlphanumeric(6))
}

processResources {
  expand project.properties
}

repositories {
  maven {
    url 'https://repo.clojars.org'
    name 'Clojars'
  }
}
