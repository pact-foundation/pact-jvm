plugins {
  id 'au.com.dius.pact.kotlin-common-conventions'
  id 'maven-publish'
  id "com.gradle.plugin-publish" version "2.0.0"
}

group = 'au.com.dius.pact.provider'
description = 'Pact-JVM - Gradle plugin for verifying pacts against a provider'

dependencies {
  implementation gradleApi()
  implementation localGroovy()
  implementation project(':provider')
  implementation project(':core:model')
  implementation 'com.github.ajalt:mordant:1.2.1'
  implementation 'commons-io:commons-io'
  implementation 'org.apache.commons:commons-lang3'
  implementation 'io.github.oshai:kotlin-logging-jvm'

  testImplementation 'junit:junit'
  testImplementation 'org.mockito:mockito-core:5.18.0'

  // This needs to match the version of Groovy provided by Gradle
  testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
    exclude group: 'org.codehaus.groovy'
  }
}

// There is a Groovy version mismatch between GroovyDoc, Gradle and the project
groovydoc {
  enabled = false
}

gradlePlugin {
  website = 'https://github.com/pact-foundation/pact-jvm/tree/master/provider/gradle'
  vcsUrl = 'https://github.com/pact-foundation/pact-jvm'
  plugins.create('pactProviderPlugin') {
      id = 'au.com.dius.pact'
      displayName = 'Gradle Pact Provider plugin'
      implementationClass = 'au.com.dius.pact.provider.gradle.PactPlugin'
      description = 'Gradle plugin for verifying pacts against a provider.'
      tags.set(['pact', 'cdc', 'consumerdrivencontracts', 'microservicetesting'])
  }
}

compileGroovy {
  dependsOn compileKotlin
  classpath = classpath.plus(files(compileKotlin.destinationDirectory))
}

publishing {
  publications {
    pluginMaven(MavenPublication) {
      pom {
        name = project.name
        description = 'Gradle Pact plugin'
        url = 'https://github.com/pact-foundation/pact-jvm'
        licenses {
          license {
            name = 'Apache 2'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }
        scm {
          url = 'https://github.com/pact-foundation/pact-jvm'
          connection = 'https://github.com/pact-foundation/pact-jvm.git'
        }

        developers {
          developer {
            id = 'thetrav'
            name = 'Travis Dixon'
            email = 'the.trav@gmail.com'
          }
          developer {
            id = 'rholshausen'
            name = 'Ronald Holshausen'
            email = 'ronald.holshausen@gmail.com'
          }
        }
      }
    }
  }
  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}
