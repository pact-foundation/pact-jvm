plugins {
  id "com.gradle.plugin-publish" version "1.0.0"
}

dependencies {
  implementation project(":provider")
  implementation gradleApi()
  implementation localGroovy()
  implementation 'com.github.ajalt:mordant:1.2.1'
  implementation 'commons-io:commons-io:2.11.0'
  implementation "org.apache.commons:commons-lang3:${project.commonsLang3Version}"

  testImplementation "junit:junit:${project.junitVersion}"
  testImplementation 'org.mockito:mockito-core:2.28.2'
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${project.junit5Version}"

  // This needs to match the version of Groovy provided by Gradle
  testImplementation('org.spockframework:spock-core:2.0-groovy-3.0') {
    exclude group: 'org.codehaus.groovy'
  }
}

// There is a Groovy version mismatch between GroovyDoc, Gradle and the project  
groovydoc {
  enabled = false
}

pluginBundle {
  website = 'https://github.com/pact-foundation/pact-jvm/tree/master/provider/gradle'
  vcsUrl = 'https://github.com/pact-foundation/pact-jvm'
  description = 'Gradle plugin for verifying pacts against a provider.'
  tags = ['pact', 'cdc', 'consumerdrivencontracts', 'microservicetesting']
}

gradlePlugin {
  plugins {
    pactProviderPlugin {
      id = 'au.com.dius.pact'
      displayName = 'Gradle Pact Provider plugin'
      implementationClass = 'au.com.dius.pact.provider.gradle.PactPlugin'
      description = 'Gradle plugin for verifying pacts against a provider.'
    }
  }
}

compileGroovy {
  dependsOn compileKotlin
  classpath = classpath.plus(files(compileKotlin.destinationDir))
}

publishing {
  publications {
    pluginMaven(MavenPublication) {
//      from components.java

      pom {
        name = project.name
        description = 'Gradle Pact plugin'
        url = 'https://github.com/pact-foundation/pact-jvm'
        licenses {
          license {
            name = 'Apache 2'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }
        scm {
          url = 'https://github.com/pact-foundation/pact-jvm'
          connection = 'https://github.com/pact-foundation/pact-jvm.git'
        }

        developers {
          developer {
            id = 'thetrav'
            name = 'Travis Dixon'
            email = 'the.trav@gmail.com'
          }
          developer {
            id = 'rholshausen'
            name = 'Ronald Holshausen'
            email = 'rholshausen@dius.com.au'
          }
        }
      }
    }
  }
  repositories {
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      if (project.hasProperty('sonatypeUsername')) {
        credentials {
          username sonatypeUsername
          password sonatypePassword
        }
      }
    }
  }
}

signing {
  required { project.hasProperty('isRelease') }
  sign publishing.publications.pluginMaven
}
