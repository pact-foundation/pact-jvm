buildscript {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath 'org.apache.commons:commons-text:1.5'
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.4.10'
//  id 'org.jmailen.kotlinter' version '3.2.0'
  id 'io.gitlab.arturbosch.detekt' version '1.14.1'
  id 'org.jetbrains.dokka' version '1.4.10' apply false
  id 'org.javamodularity.moduleplugin' version '1.7.0' apply false
}

import org.apache.commons.text.StringEscapeUtils

allprojects {
  apply plugin: 'maven-publish'
  apply plugin: 'signing'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  targetCompatibility = '11'
  sourceCompatibility = '11'
}

subprojects {

  if (name in ['core']) {
    return
  }

  version = '4.2.0-beta.2'
  
  buildscript {
    repositories {
      jcenter()
      mavenCentral()
      mavenLocal()
    }
  }

  configurations {
    groovyDoc
  }

  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'codenarc'
  if (!System.env.CI) {
    apply plugin: 'jacoco'
  }
  apply plugin: 'org.jetbrains.kotlin.jvm'
//  apply plugin: 'org.jmailen.kotlinter'
  apply plugin: 'io.gitlab.arturbosch.detekt'
  if (System.env.TRAVIS != 'true') {
    apply plugin: 'org.jetbrains.dokka'
  }
  apply plugin: "org.javamodularity.moduleplugin"

  dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  }

  java {
    modularity.inferModulePath = true
  }

    compileTestGroovy {
      classpath = classpath.plus(files(compileTestKotlin.destinationDir))
      dependsOn compileTestKotlin
    }

    tasks.withType(GroovyCompile) {
      groovyOptions.optimizationOptions.indy = true
    }

    tasks.withType(Groovydoc) {
      groovyClasspath = project.configurations.groovyDoc
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
      kotlinOptions {
        jvmTarget = "11"
      }
    }

    jar {
        manifest {
          attributes provider: 'gradle',
            'Implementation-Title': project.name, 'Implementation-Version': archiveVersion,
            'Implementation-Vendor': project.group, 'Implementation-Vendor-Id': project.group,
            'Specification-Vendor': project.group,
            'Specification-Title': project.name,
            'Specification-Version': archiveVersion,
            'Automatic-Module-Name': project.group + "." + project.name.replaceAll('-', '_')
        }
    }

  if (System.env.CI != 'true') {
    task javadocJar(type: Jar, dependsOn: [javadoc, groovydoc, dokkaJavadoc]) {
      archiveClassifier = 'javadoc'
      from javadoc.destinationDir, groovydoc.destinationDir, dokkaJavadoc.outputDirectory
    }
  } else {
    task javadocJar(type: Jar, dependsOn: [javadoc, groovydoc]) {
      archiveClassifier = 'javadoc'
      from javadoc.destinationDir, groovydoc.destinationDir
    }
  }

    task sourceJar(type: Jar) {
      archiveClassifier = 'sources'
      from sourceSets.main.allSource
    }

  test {
    useJUnitPlatform()

    if (System.env.CI == 'true') {
      testLogging {
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams false
        events "passed", "skipped", "failed"
      }
    } else {
      // Show test results.
      testLogging {
        events "passed", "skipped", "failed"
      }
    }
  }

  publishing {
    publications {
      mavenPublication(MavenPublication) {
        from components.java
        artifact sourceJar
        artifact javadocJar
        pom {
          name = project.name
          description = StringEscapeUtils.escapeXml11(new File(projectDir, 'README.md').text)
          url = 'https://github.com/DiUS/pact-jvm'
          licenses {
            license {
              name = 'Apache 2'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
          scm {
            url = 'https://github.com/DiUS/pact-jvm'
            connection = 'https://github.com/DiUS/pact-jvm.git'
          }

          developers {
            developer {
              id = 'thetrav'
              name = 'Travis Dixon'
              email = 'the.trav@gmail.com'
            }
            developer {
              id = 'rholshausen'
              name = 'Ronald Holshausen'
              email = 'rholshausen@dius.com.au'
            }
          }
        }
      }
    }
    repositories {
      maven {
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        if (project.hasProperty('sonatypeUsername')) {
          credentials {
            username sonatypeUsername
            password sonatypePassword
          }
        }
      }
    }
  }

  if (System.env.CI != 'true') {
    signing {
      sign publishing.publications.mavenPublication
    }
  }

  codenarc {
    toolVersion = "1.6.1"
  }

  codenarcMain {
      configFile = rootProject.file('config/codenarc/ruleset.groovy')
  }

  codenarcTest {
      configFile = rootProject.file('config/codenarc/rulesetTest.groovy')
  }

  if (!System.env.CI) {
    check.dependsOn << 'jacocoTestReport'
  }

//  kotlinter {
//    indentSize = 2
//    reporters = ['checkstyle', 'plain', 'json']
//  }

  detekt {
    failFast = false
    config = files(rootProject.file("config/detekt-config.yml"))
  }

  task allDeps(type: DependencyReportTask) {}
}
