buildscript {
  repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath 'org.apache.commons:commons-text:1.5'
  }
}

plugins {
  id 'nebula.kotlin' version '1.3.30'
//  id 'org.jmailen.kotlinter' version '1.23.1'
  id 'io.gitlab.arturbosch.detekt' version '1.0.0-RC14'
  id 'org.jetbrains.dokka' version '0.9.17'
  id 'com.github.johnrengelman.shadow' version '5.0.0' apply false
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

import org.apache.commons.text.StringEscapeUtils

String buildName(Project project) {
  if (project.name.startsWith('pact-jvm')) {
    project.name
  } else {
    if (project.parent) {
      buildName(project.parent) + '-' + project.name
    } else {
      project.name
    }
  }
}

allprojects {
  apply plugin: 'maven-publish'
  if (System.env.TRAVIS != 'true') {
    apply plugin: 'signing'
  }

  group = 'au.com.dius'
  version = '4.0.0-beta.3'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  targetCompatibility = '1.8'
  sourceCompatibility = '1.8'
}

subprojects {

  if (name in ['core', 'consumer', 'provider']) {
    return
  }
  
  buildscript {
    repositories {
      jcenter()
      mavenCentral()
      mavenLocal()
    }
  }

  configurations {
    groovyDoc
  }


  apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'codenarc'
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.kotlin.jvm'
//    apply plugin: 'org.jmailen.kotlinter'
    apply plugin: 'io.gitlab.arturbosch.detekt'
    apply plugin: 'org.jetbrains.dokka'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
      testCompile "org.junit.jupiter:junit-jupiter-api:${project.junit5Version}"
      testRuntime "org.junit.jupiter:junit-jupiter-engine:${project.junit5Version}"
      testCompile 'org.hamcrest:hamcrest:2.1'
      testCompile('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
      }
      testCompile 'net.bytebuddy:byte-buddy:1.9.13'
      testCompile 'org.objenesis:objenesis:3.0.1'
      testCompile "org.codehaus.groovy:groovy:${project.groovyVersion}"

      groovyDoc "org.codehaus.groovy:groovy-all:${project.groovyVersion}"
    }

    compileTestGroovy {
      classpath = classpath.plus(files(compileTestKotlin.destinationDir))
      dependsOn compileTestKotlin
    }

    tasks.withType(GroovyCompile) {
      groovyOptions.optimizationOptions.indy = true
    }

    tasks.withType(Groovydoc) {
      groovyClasspath = project.configurations.groovyDoc
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
      kotlinOptions {
        jvmTarget = "1.8"
      }
    }

    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
      if (JavaVersion.current().java9Compatible) {
        enabled = false
      }
      outputFormat = 'javadoc'
      outputDirectory = "$buildDir/docs/dokkaJavadoc"
    }

    jar {
        manifest {
          attributes provider: 'gradle',
            'Implementation-Title': project.name, 'Implementation-Version': version,
            'Implementation-Vendor': project.group, 'Implementation-Vendor-Id': project.group,
            'Specification-Vendor': project.group,
            'Specification-Title': project.name,
            'Specification-Version': version,
            'Automatic-Module-Name': project.group + "." + project.name
        }
    }

    task javadocJar(type: Jar, dependsOn: [javadoc, groovydoc, dokkaJavadoc]) {
      archiveClassifier = 'javadoc'
      from javadoc.destinationDir, groovydoc.destinationDir, dokkaJavadoc.outputDirectory
    }

    task sourceJar(type: Jar) {
      archiveClassifier = 'sources'
      from sourceSets.main.allSource
    }

  test {
    useJUnitPlatform()

    // Show test results.
    testLogging {
      events "passed", "skipped", "failed"
    }
  }

  publishing {
    publications {
      mavenPublication(MavenPublication) {
        from components.java
        artifact sourceJar
        artifact javadocJar
        pom {
          name = project.name
          description = StringEscapeUtils.escapeXml11(new File(projectDir, 'README.md').text)
          url = 'https://github.com/DiUS/pact-jvm'
          licenses {
            license {
              name = 'Apache 2'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
          scm {
            url = 'https://github.com/DiUS/pact-jvm'
            connection = 'https://github.com/DiUS/pact-jvm.git'
          }

          developers {
            developer {
              id = 'thetrav'
              name = 'Travis Dixon'
              email = 'the.trav@gmail.com'
            }
            developer {
              id = 'rholshausen'
              name = 'Ronald Holshausen'
              email = 'rholshausen@dius.com.au'
            }
          }
        }
      }
    }
    repositories {
      maven {
        url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        if (project.hasProperty('sonatypeUsername')) {
          credentials {
            username sonatypeUsername
            password sonatypePassword
          }
        }
      }
    }
  }

  if (System.env.TRAVIS != 'true') {
    signing {
      sign publishing.publications.mavenPublication
    }
  }

    codenarcMain {
        configFile = rootProject.file('config/codenarc/ruleset.groovy')
    }

    codenarcTest {
        configFile = rootProject.file('config/codenarc/rulesetTest.groovy')
    }

    check.dependsOn << 'jacocoTestReport'

//  kotlinter {
//    indentSize = 2
//    continuationIndentSize = 2
//    reporters = ['checkstyle', 'plain', 'json']
//  }

  detekt {
    config = files(rootProject.file("config/detekt-config.yml"))
  }

  task allDeps(type: DependencyReportTask) {}
}

if (System.env.TRAVIS == 'true') {
    allprojects {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            minHeapSize = '128m'
            maxHeapSize = '768m'
        }
    }
}
