plugins {
  id "com.gradle.plugin-publish" version "0.9.7"
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'

dependencies {
  compile project(":pact-jvm-provider")
  testCompile 'org.powermock:powermock-module-junit4:1.7.3'
  testCompile 'org.powermock:powermock-api-mockito2:1.7.3'
  testCompile 'org.mockito:mockito-core:2.8.0'
  compile "org.fusesource.jansi:jansi:${project.jansiVersion}"
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java

      artifact sourceJar {
        classifier "sources"
      }
      artifact javadocJar {
        classifier "javadoc"
      }
    }
  }
}

pluginBundle {
  website = 'https://github.com/DiUS/pact-jvm/tree/master/pact-jvm-provider-gradle'
  vcsUrl = 'https://github.com/DiUS/pact-jvm.git'
  description = 'Gradle plugin for verifying pacts against a provider.'
  tags = ['pact', 'provider', 'consumerdrivencontracts', 'microservicetesting']

  plugins {
    pactProviderPlugin {
      id = 'au.com.dius.pact'
      displayName = 'Gradle Pact Provider plugin'
    }
  }
}

test {

  // exclude the gradle version of jansi from the classpath
  classpath = project.sourceSets.test.runtimeClasspath.filter {
    it.name != 'jansi-1.2.1.jar'
  }

}
